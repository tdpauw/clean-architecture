subprojects {
  apply plugin: 'java'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
      mavenCentral()
  }

  dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.5.2'
  }
}

project(':app-api') {
  dependencies {
    compile project(':domain')
  }
}

project(':app-impl') {
  dependencies {
    compile project(':app-api')
    compile project(':domain')
    testCompile project(path:':domain', configuration:'testOutput')
    testCompile 'org.mockito:mockito-all:1.10.19'
  }
}

project(':domain') {
  task jarTest (type: Jar) {
    from sourceSets.test.output
    classifier = 'test'
  }

  configurations {
    testOutput
  }

  artifacts {
    testOutput jarTest
  }
}

project(':jpa-adapter') {
  dependencies {
    compileOnly 'javax:javaee-web-api:7.0'
    compile project(':app-api')
    testCompile project(path:':domain', configuration:'testOutput')
    testCompile 'org.liquibase:liquibase-core:3.5.0'
    testCompile 'commons-beanutils:commons-beanutils:1.9.2'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.hibernate:hibernate-core:5.2.3.Final'
    testRuntime 'org.hsqldb:hsqldb:2.3.4'
  }
}

project(':infra-rest-webbit') {
  dependencies {
    compileOnly 'javax:javaee-web-api:7.0'
    compile project(':app-impl')
    compile project(':jpa-adapter')
    compile 'org.webbitserver:webbit:0.4.15'
    runtime 'org.hibernate:hibernate-core:5.2.3.Final'
  }
}

project(':infra-rest-vertx') {
  dependencies {
    compileOnly 'javax:javaee-web-api:7.0'
    compile project(':app-impl')
    compile project(':jpa-adapter')
    compile 'io.vertx:vertx-core:3.3.3'
    compile 'io.vertx:vertx-web:3.3.3'
    runtime 'org.hibernate:hibernate-core:5.2.3.Final'
    testCompileOnly 'javax:javaee-web-api:7.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'io.vertx:vertx-unit:3.3.3'
    testCompile'org.liquibase:liquibase-core:3.5.0'
    testRuntime 'org.hsqldb:hsqldb:2.3.4'
  }
}

project(':acceptance-tests') {
  dependencies {
    compile 'org.seleniumhq.selenium:selenium-java:2.52.0'
    compile 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.52.0'
  }
}
